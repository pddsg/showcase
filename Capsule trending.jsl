/*

Author:					Patrick Galler
Creation date:			09.Aug.'24
Creation JMP version:	JMP 17.2.0


Description: Get all QC control sample results for tests managed via LIMS in Houston. Calculate relative overfill for spec parameters and create various trend graphs.

Version changes:
	v1: 	first version
	v1.4: 	added monophasic NMR PL test parameters for Lysoveta capsules
	v1.5: 	added Best Formulations to ManufacturingSites
			added 'Fatty Profile (12)', 'Peroxide value (6)', 'p-Anisidine value' and 'TOTOX value'
				these are all specific to algae oil at the time of writing
			added 'Average fill weight (3)'
				this is specific to Swisscaps Lysoveta capsule bx ID 1533140100 at the time of writing
			hard coded mfg date for bx 230334 into script since this is missing in DWH
	v1.6:	changed ManufacturingSite 'Swisscaps' to 'Aenova Romania S.R.L.' for t1
			added table script for graphing all QC results to be able to also plot peroxide value, time to disintegratoin & more
			added labelling of additional columns to provide more pop-up info in graphs
			event handler for finding batch qc release data from `akbm-datawarehouse-operational.views_qualityControl.FactQCFullHistory`
				using batchid or LIMS sampleid
	v1.7:	recoding of "Spec-00346 (US), Spec-00269" to "Spec-00269" in CapsQCrslts:ProductSpecificationNumber introduced to allow for joining with spec limit table				

*/

Names Default To Here( 1 );
//-------------------------------------------------------------------------------------------------
// getting data needed with a relatively general SQL query requiring further clean-up down the line.
// this query pulls only rows 'WHERE Source = 'LIMS'' because rows for Jeeves only contain data from roughly 2012-2015.
// column 'PropertyName' was not included because it is redundant w column 'TestName'
// '(Unit != 'A%' OR Unit IS NULL)' excludes area% rows from fatty acid quant while also retaining rows where the unit is missing
//-------------------------------------------------------------------------------------------------
CapsQCrslts = Open Database( "DSN=bq64_system;", 
	"
		SELECT 
			t1.Source,
			t1.SampleType,
			t1.ManufacturingSite,
			t1.BatchId,
			t1.SampleId,
			t1.ItemNumber,
			t1.Product,
			t1.CommercialProduct,
			t1.TestDate,
			t1.TestName,
			t1.Method,
			t1.TestDetail,
			t1.ParamType,
			t1.Value,
			t1.TextValue,
			t1.Unit,
			t1.CompletedBy,
			t1.ReplicateId,
			t1.Dataset,
			t2.ProductSpecificationNumber,
			min_MfgDates.min_MfgDate

		FROM 
			`akbm-datawarehouse-operational.views_qualityControl.FactQCFullHistory` AS t1
		
		JOIN
			`akbm-datawarehouse-operational.views_mdm.DimItem` AS t2 ON t1.ItemNumber = t2.ItemNumber 
		
		## subquery for getting only minimum, i.e. oldest mfg dates in case there are multiple dates	
		LEFT JOIN (
			SELECT
				BatchId,
				MIN(ManufacturingDate) as min_MfgDate
				
			FROM
				`akbm-datawarehouse-operational.views_jeeves.FactInventory` 
			
			GROUP BY
			BatchId
	
		) AS min_MfgDates ON t1.BatchId = min_MfgDates.BatchId
				
		WHERE 
			t1.ManufacturingSite IN
				(	'Aenova Romania S.R.L.',
					'Best Formulations Inc',
					'Captek',
					'Catalent Ontario Limited',
					'International Nutrient Technologies Limited',
					'Lonza Greenwood LLC',
					'Unicaps'
				)
				
		AND 
			t1.Source = 'LIMS' ## everything below in this query is so specific that it doesn't seem to matter whether only rows for source LIMS are retrieved.
		
		AND
			t1.Value IS NOT NULL
		
		AND ## this gets the different qc tests
		(
			(	t1.TestName = 'Astaxanthin (2)' AND 
				t1.TestDetail IN(
					'Astaxanthin',
					'Capsule fill weight'
				)  AND 
				t1.ParamType = 'Standard'
			)
			
			OR
			(	t1.TestName = 'Esterified astaxanthin (2)' AND 
				t1.TestDetail = 'Esterified astaxanthin' AND 
				t1.ParamType = 'Standard'
			)
			
			OR
			(	t1.TestName = 'Esterified Astaxantin (as astaxanthin)' AND 
				t1.TestDetail IN (
					'Esterified Astaxantin (as Astaxanthin)',
					'Capsule fill weight'
				) AND 
				t1.ParamType = 'Standard'
			)
			
			OR
			TestName = 'Average fill weight' AND
			TestDetail = 'Average fill weight'

			OR
			TestName = 'Average fill weight (3)' AND
			TestDetail = 'Average fill weight'
			
			OR
			TestName = 'Average fill weight (4)' AND
			TestDetail = 'Average fill weight'
			
			OR
			(	t1.TestName = 'Fatty acid profile (6)' AND
				t1.TestDetail IN(
					'C20:5n3 (EPA)',
					'EPA (EE)',
					'EPA (TG)',
					'C22:6n3 (DHA)',
					'DHA (EE)',
					'DHA (TG)',
					'EPA + DHA',
					'EPA (EE) + DHA (EE)',
					'EPA (TG) + DHA (TG)',
					'PUFA n-3',
					'Total omega-3 fatty acids  (EE)',
					'Total omega-3 fatty acids (TG)',
					'Capsule fill weight'
				) AND
				t1.ParamType = 'Standard'
			)

			OR
			(	t1.TestName = 'Fatty acid profile (12)' AND
				t1.TestDetail IN(
					'C20:5n3 (EPA)',
					'EPA (EE)',
					'EPA (TG)',
					'C22:6n3 (DHA)',
					'DHA (EE)',
					'DHA (TG)',
					'EPA + DHA',
					'EPA (EE) + DHA (EE)',
					'EPA (TG) + DHA (TG)',
					'PUFA n-3',
					'Total omega-3 fatty acids  (EE)',
					'Total omega-3 fatty acids (TG)',
					'Capsule fill weight'
				) AND
				t1.ParamType = 'Standard'
			)
			
			OR
			(	t1.TestName = 'Phospholipids (2)' AND
				t1.TestDetail IN(
					'1-LPC',
					'2-LPC',
					'APE',
					'Choline',
					'LPA',
					'LPE',
					'Other PL',
					'PC',
					'PE',
					'PI',
					'Phosphatidylcholine',
					'Sum (Total PL)'
				) AND
				t1.ParamType = 'Standard'
			)
			
			OR
				t1.TestName = 'Phospholipids (2)' AND
				t1.TestDetail = 'Number of capsules used for testing'
			
			OR
				t1.TestName = 'Phospholipids (2)' AND
				t1.TestDetail = 'Phosphorus'
			
			OR
			(	t1.TestName = 'Phospholipids (5)' AND
				t1.TestDetail IN(
					'1-LPC',
					'2-LPC',
					'APE',
					'Choline',
					'LPA',
					'LPE',
					'Other PL',
					'PC',
					'PE',
					'PI',
					'Phosphatidylcholine',
					'Sum (Total PL)',
					'Total Lysophosphatidylcholines'
				) AND
				t1.ParamType = 'Standard'
			)
			
			OR
				t1.TestName = 'Phospholipids (5)' AND
				t1.TestDetail = 'Number of capsules used for testing'
			
			OR
				t1.TestName = 'Phospholipids (5)' AND
				t1.TestDetail = 'Phosphorus'
			
			OR
				t1.TestName = 'Phospholipids (5)' AND
				t1.TestDetail = 'GPC'
			
			OR
				t1.TestName = 'Peroxide value' AND
				t1.TestDetail = 'Peroxide value' AND
				t1.ParamType = 'Standard'

			OR
				t1.TestName = 'Peroxide value (6)' AND
				t1.TestDetail = 'Peroxide value' AND
				t1.ParamType = 'Standard'

			OR
				t1.TestName = 'p-Anisidine value' AND
				t1.TestDetail = 'p-Anisidine value' AND
				t1.ParamType = 'Standard'

			OR
				t1.TestName = 'TOTOX value' AND
				t1.TestDetail = 'TOTOX value' AND
				t1.ParamType = 'Standard'
			
			OR
				t1.TestName = 'Disintegration' AND
				t1.TestDetail IN(
					'Disintegration pass/fail',
					'Disintegration minutes'
				)
				
			OR
				t1.TestName = 'Disintegration (2)' AND
				t1.TestDetail IN(
					'Disintegration pass/fail',
					'Disintegration minutes'
				)
			
			OR
				t1.TestName = 'Rupture' AND
				t1.TestDetail IN(
					'Rupture pass/fail',
					'Rupture minutes'
				)
		)
	",
	//Invisible,
	"QCData"
);
CapsQCrslts << Set Label Columns( :BatchId, :Product, :ItemNumber, :ManufacturingSite, :SampleId );
CapsQCrslts << Select Duplicate Rows() << Delete Rows;
//-------------------------------------------------------------------------------------------------
// recoding test names where adequate
//-------------------------------------------------------------------------------------------------
CapsQCrslts << Begin Data Update;
	CapsQCrslts << Recode Column(
		CapsQCrslts:TestName,
		{Map Value(
			_rcOrig,
			{	"Average fill weight (4)", 					"Average fill weight",
				"Average fill weight (3)", 					"Average fill weight",
				"Disintegration (2)", 						"Disintegration"
			},
			Unmatched( _rcNow )
		)},
		Update Properties( 1 ),
		Target Column( :TestName )
	);
CapsQCrslts << End Data Update;
//-------------------------------------------------------------------------------------------------	
// introducing a new column for recoded qc test (paramid) names for trending
//-------------------------------------------------------------------------------------------------
CapsQCrslts << New Column( 
	"TrendParam", Character, Nominal,
	Formula( 
		If( 
			:TestDetail == "Capsule fill weight",		:TrendParam = :TestDetail || " from " || :TestName, 
			:TrendParam = :TestDetail, 
		)	
	)
);
//-------------------------------------------------------------------------------------------------	
// getting spec limits for joining with capsule qc release test results.
// we could probably combine this with the SQL query at the top, in which case however we'd need to identify the latest
// spec version using a string variable
//-------------------------------------------------------------------------------------------------	
RawSpecs = Open Database( "DSN=bq64_system;", 
	"
		SELECT * 
		
		FROM 
		
			`akbm-datawarehouse-operational.raw_labvantage.raw_specification`
		
		WHERE 
		
			(paramid IN
				(	'Astaxanthin',
					'Average fill weight',
					'Average fill weight of krill oil',
					'C20:5n3 (EPA)',
					'C22:6n3 (DHA)',
					'Choline',
					'DHA (EE)',
					'DHA (TG)',
					'Disintegration minutes',
					'EPA (EE)',
					'EPA (EE) + DHA (EE)',
					'EPA (TG)',
					'EPA (TG) + DHA (TG)',
					'EPA + DHA',
					'Esterified astaxanthin',
					'Esterified Astaxantin (as Astaxanthin)',
					'Fill weight of astaxanthin (10%)',
					'Phosphatidylcholine',
					'PUFA n-3',
					'Rupture minutes',
					'Sum (Total PL)',
					'Total Lysophosphatidylcholines',
					'Total omega-3 fatty acids  (EE)',
					'Total omega-3 fatty acids (TG)'
				)
			)
		
		AND	
		
			value1 IS NOT NULL
		
		AND 
		
			(unitsid NOT IN ('A%', 'g/100 g', 'mg/kg', 'μg/g'))",
		
		//Invisible, 
		"rawSpecs"
);
//-------------------------------------------------------------------------------------------------	
// adding mfg date for bx id 230334. this is missing in the DWH and was taken from Box pdf CoA
//-------------------------------------------------------------------------------------------------
idx230334 = CapsQCrslts << Get Rows Where(:BatchId == "230334");
Column(CapsQCrslts, "min_MfgDate")[idx230334] = 3758745600; // this nr represents 09.Feb.'24
//-------------------------------------------------------------------------------------------------	
// changing the data & modeling types of a few columns that could be numerical data in DWH but aren't (create ticket?)
//-------------------------------------------------------------------------------------------------
RawSpecs:specversionid << Data Type ( Numeric );
RawSpecs:value1 	   << Data Type ( Numeric );
RawSpecs:value2        << Data Type ( Numeric );
RawSpecs:specversionid << Set Modeling Type ( "Continuous" );
RawSpecs:value1 	   << Set Modeling Type ( "Continuous" );
RawSpecs:value2        << Set Modeling Type ( "Continuous" );
//-------------------------------------------------------------------------------------------------	
// keep only the highest specversionid, i.e., the latest spec for a product
//-------------------------------------------------------------------------------------------------
RawSpecs << Select Where( RawSpecs:specversionid < Col Max( RawSpecs:specversionid, :specid ) ) << Delete Rows;
//-------------------------------------------------------------------------------------------------	
// recoding ProductSpecificationNumber in QC data table
// this may be the first occurrence of a deviation from the 1:1 relationship between item nr to product spec nr 
//-------------------------------------------------------------------------------------------------
CapsQCrslts << Begin Data Update;
For Each Row(
	CapsQCrslts,
	CapsQCrslts:ProductSpecificationNumber[] = Map Value(
		CapsQCrslts:ProductSpecificationNumber,
		{"Spec-00346 (US), Spec-00269", "Spec-00269"},
		Unmatched( CapsQCrslts:ProductSpecificationNumber )
	)
);
CapsQCrslts << End Data Update;
//-------------------------------------------------------------------------------------------------	
// join qc release test results with spec limits
//-------------------------------------------------------------------------------------------------
RsltNLmts = CapsQCrslts << Join(
	With( RawSpecs ),
	By Matching Columns(
		:ProductSpecificationNumber = :specid, :TrendParam = :paramid
	),
	Drop multiples( 0, 0 ),
	Include Nonmatches( 1, 0 ),
	Preserve main table order( 1 ),
	Output Table( "QC test results and spec limits" )
);
Close( CapsQCrslts, no save );
Close( RawSpecs, no save );
//-------------------------------------------------------------------------------------------------	
// deleting table scripts
//-------------------------------------------------------------------------------------------------
Lst_Scripts = RsltNLmts << Get Table Script Names;
RsltNLmts << Delete Scripts( Lst_Scripts );
//-------------------------------------------------------------------------------------------------	
// sorting the table
//-------------------------------------------------------------------------------------------------
RsltNLmts << Sort( By( :min_MfgDate, :TestName ), Replace Table, Order( Ascending, Ascending ) );
//-------------------------------------------------------------------------------------------------	
// renaming columns
//-------------------------------------------------------------------------------------------------
RsltNLmts:Value << Set Name( "Release rslt" );
RsltNLmts:value1 << Set Name( "Spec lmt 1" );
RsltNLmts:value2 << Set Name( "Spec lmt 2" );
//-------------------------------------------------------------------------------------------------	
// formula column: calculate overfill relative to lower spec limit
//-------------------------------------------------------------------------------------------------
RsltNLmts << New Column( 
	"Overfill, lower limit [%rel]", Numeric, Continous, Format( "Fixed Dec", 10, 1 ),
	Formula( 
		If( 
			:limittypeid == "Lower OutOfSpec",		:"Overfill, lower limit [%rel]"n = ("Release rslt"n/:"Spec lmt 1"n)*100-100, 
		)	
	)
);
//-------------------------------------------------------------------------------------------------	
// table script (graph): overfill % vs. mfg date, by CMO
//-------------------------------------------------------------------------------------------------
RsltNLmts << New Script( "Overfill, relative to lower spec vs mfg date, by CMO",
	Graph Builder(
		Size( 657, 372 ),
		Show Control Panel( 0 ),
		Fit to Window( "Off" ),
		Variables(
			X( :min_MfgDate ),
			Y( :"Overfill, lower limit [%rel]"n ),
			Overlay( :ManufacturingSite )
		),
		Elements( Points( X, Y, Legend( 7 ) ) ),
		Local Data Filter(
			Add Filter( columns( :paramid ), Display( :paramlistid, N Items( 9 ) ) )
		),
		SendToReport(
			Dispatch(
				{},
				"Overfill, lower limit [%rel]",
				ScaleBox,
				{Add Ref Line( 0, "Solid", "Black", "", 1 )}
			)
		)
	)
);
//-------------------------------------------------------------------------------------------------	
// table script (graph): overfill % vs. mfg date, by Commercial Product
//-------------------------------------------------------------------------------------------------
RsltNLmts << New Script( "Overfill, relative to lower spec vs mfg date, by Commerical Product",
	Graph Builder(
		Size( 657, 372 ),
		Show Control Panel( 0 ),
		Fit to Window( "Off" ),
		Variables(
			X( :min_MfgDate ),
			Y( :"Overfill, lower limit [%rel]"n ),
			Overlay( :CommercialProduct )
		),
		Elements( Points( X, Y, Legend( 7 ) ) ),
		Local Data Filter(
			Add Filter( columns( :paramid ), Display( :paramlistid, N Items( 9 ) ) )
		),
		SendToReport(
			Dispatch(
				{},
				"Overfill, lower limit [%rel]",
				ScaleBox,
				{Add Ref Line( 0, "Solid", "Black", "", 1 )}
			)
		)
	)
);
//-------------------------------------------------------------------------------------------------	
// table script (graph): overfill vs paramid, by batch id
//-------------------------------------------------------------------------------------------------
RsltNLmts << New Script( "Overfill, relative to lower spec vs paramid, by batchid",
	Graph Builder(
		Size( 863, 645 ),
		Show Control Panel( 0 ),
		Fit to Window( "Off" ),
		Variables(
			X(
				:paramid,
				Order By(
					:"Overfill, lower limit [%rel]"n,
					Ascending,
					Order Statistic( "Mean" )
				)
			),
			Y( :"Overfill, lower limit [%rel]"n ),
			Overlay( :BatchId )
		),
		Elements(
			Points( X, Y, Legend( 3 ), Jitter Limit( 0 ) ),
			Line( X, Y, Legend( 4 ) )
		),
		Local Data Filter(
			Add Filter(
				columns(
					:BatchId, :ManufacturingSite, :"Overfill, lower limit [%rel]"n
				),
				Display( :BatchId, N Items( 10 ), Find( Set Text( "" ) ) ),
				Display( :ManufacturingSite, N Items( 5 ) )
			)
		),
		SendToReport(
			Dispatch(
				{},
				"paramid",
				ScaleBox,
				{Label Row( Label Orientation( "Angled" ) )}
			),
			Dispatch(
				{},
				"Overfill, lower limit [%rel]",
				ScaleBox,
				{Add Ref Line( 0, "Solid", "Black", "", 1 )}
			)
		)
	)		
);
//-------------------------------------------------------------------------------------------------	
// table script (graph): overfill vs CMO, by paramid - box plot
//-------------------------------------------------------------------------------------------------
RsltNLmts << New Script( "Overfill, relative to lower spec vs CMO, by paramid, box plot",
	Graph Builder(
		Size( 534, 464 ),
		Show Control Panel( 0 ),
		Fit to Window( "Off" ),
		Variables(
			X(
				:ManufacturingSite,
				Order By(
					:"Overfill, lower limit [%rel]"n,
					Ascending,
					Order Statistic( "Mean" )
				)
			),
			Y( :"Overfill, lower limit [%rel]"n ),
			Overlay( :paramid )
		),
		Elements( Points( X, Y, Legend( 5 ) ), Box Plot( X, Y, Legend( 8 ) ) ),
		Local Data Filter(
			Add Filter(
				columns( :paramid, :"Overfill, lower limit [%rel]"n ),
				// Where( :paramid == "Astaxanthin" ), // this could do with a list index rather than outright name
				Display( :paramid, N Items( 10 ) )
			)
		),
		SendToReport(
			Dispatch( {}, "400", LegendBox, {Legend Position( {5, [0], 8, [-1, -3]} )} )
		)
	)
);
//-------------------------------------------------------------------------------------------------	
// table script (graph): overfill vs CMO, by paramid
//-------------------------------------------------------------------------------------------------
RsltNLmts << New Script( "Overfill, relative to lower spec vs CMO, by paramid",
	Graph Builder(
		Size( 534, 464 ),
		Show Control Panel( 0 ),
		Fit to Window( "Off" ),
		Variables(
			X(
				:ManufacturingSite,
				Order By(
					:"Overfill, lower limit [%rel]"n,
					Ascending,
					Order Statistic( "Mean" )
				)
			),
			Y( :"Overfill, lower limit [%rel]"n ),
			Overlay( :paramid )
		),
		Elements( Points( X, Y, Legend( 4 ) ) ),
		Local Data Filter(
			Add Filter(
				columns( :paramid, :"Overfill, lower limit [%rel]"n ),
				Display( :paramid, N Items( 10 ) )
			)
		)
	)
);
//-------------------------------------------------------------------------------------------------	
// table script (graph): general trending of all QC test results, incl. peroxides, time to disintegration, rupture, etc.
//-------------------------------------------------------------------------------------------------
RsltNLmts << New Script( "Trending all QC results",
	Graph Builder(
		Size( 571, 363 ),
		Show Control Panel( 0 ),
		Fit to Window( "Off" ),
		Variables( X( :min_MfgDate ), Y( :Release rslt ), Color( :CommercialProduct ) ),
		Elements( Points( X, Y, Legend( 7 ) ) ),
		Local Data Filter(
			Add Filter(
				columns( :TestDetail, :CommercialProduct, :Product, :ManufacturingSite ),
				Where( :TestDetail == "Peroxide value" ),
				Display( :TestDetail, N Items( 15 ), Find( Set Text( "" ) ) ),
				Display( :CommercialProduct, N Items( 8 ) ),
				Display( :Product, N Items( 15 ) ),
				Display( :ManufacturingSite, N Items( 7 ) )
			)
		)
	);
);
//-------------------------------------------------------------------------------------------------	
// table script (subset, statistics & graphing): show LPC/PC ratio over time
//-------------------------------------------------------------------------------------------------
Eval(
	EvalExpr( 
		RsltNLmts << New Script( "LPC/PC ratios vs mfg date",
			Expr( RsltNLmts ) << Clear Column Selection; // without this, there is an issue with this table script if a col is selected
			Expr( RsltNLmts ) << Clear Select;  // just to be sure, clear also row selection
			LPCPCsbst = Expr( RsltNLmts ) << Subset( 
				Filtered Rows(
					:TrendParam == "1-LPC" | // OR operator
					:TrendParam == "2-LPC" | 
					:TrendParam == "PC"
				)
			);
			// split the subset
			LPCPCsplt = LPCPCsbst << Split(
				Split By( :TrendParam ),
				Split( :Release rslt ),
				Group( 	:BatchId, 
						//:ItemNumber, // avoids duplicates. remember, that a relabeled product carries with it its release result.
						:Product, 
						:CommercialProduct,
						:ManufacturingSite,
						:min_MfgDate,
						:ReplicateId,
						:Dataset
				),
				Output Table( "LPC/PC ratios vs mfg date" ),
				Remaining Columns( Drop All ),
				Sort by Column Property
			);
			// close the original subset
			Close( LPCPCsbst, no save );
			// sorting by mfg date
			LPCsrt = LPCPCsplt << Sort(
				By( :min_MfgDate ),
				Replace Table,
				Order( Ascending )
			);
			// delete table scripts in subset table
			Lst_Scripts2 = LPCsrt << Get Table Script Names;
			LPCsrt << Delete Scripts( Lst_Scripts2 );
			// calculate lpc/pc ratio
			LPCsrt << New Column( "LPC/PC ratio", Numeric, Continous, Format( "Fixed Dec", 10, 1 ),
				Formula( (:"1-LPC"n + :"2-LPC"n) / :PC )
			);
			// add table script for graphing lpc/pc ratio
			LPCsrt << New Script( "LPC/PC ratio vs mfg date, by prod",
				Graph Builder(
					Size( 534, 460 ),
					Show Control Panel( 0 ),
					Fit to Window( "Off" ),
					Variables(
						X( :min_MfgDate ),
						Y( :"LPC/PC ratio"n ),
						Color( :CommercialProduct )
					),
					Elements(
						Points( X, Y, Legend( 3 ) ),
						Line Of Fit( X, Y, Legend( 5 ), Confidence of Prediction( 1 ) )
					),
					SendToReport(
						Dispatch(
							{},
							"400",
							LegendBox,
							{Legend Position( {3, [0, 1, 2, 3, 4], 5, [-1, -3, -3]} )}
						)
					)
				)
			);
			// add another table script for graphing lpc/pc ratio
			LPCsrt << New Script( "LPC/PC ratio vs mfg date, by cmo",
				Graph Builder(
					Size( 534, 460 ),
					Show Control Panel( 0 ),
					Fit to Window( "Off" ),
					Variables(
						X( :min_MfgDate ),
						Y( :"LPC/PC ratio"n ),
						Color( :ManufacturingSite )
					),
					Elements(
						Points( X, Y, Legend( 3 ) ),
						Line Of Fit( X, Y, Legend( 5 ), Confidence of Prediction( 1 ) )
					),
					SendToReport(
						Dispatch(
							{},
							"400",
							LegendBox,
							{Legend Position( {3, [0, 1, 2, 3, 4], 5, [-1, -3, -3]} )}
						)
					)
				)
			);
			// this is what sets the width of the table script panel			
			Wait( 0 ); 
			window = Get Window( LPCsrt );
			sb = window << Find( SplitterBox( 1 ) );
			sb << Set Width( 250 );
		)
	)
);
//-------------------------------------------------------------------------------------------------	
// adjusting table script panel width & height
//-------------------------------------------------------------------------------------------------
// width
Wait( 0 ); // without this, the width adjustment isn't executed on the table
window = Get Window( RsltNLmts );
sb2 = window << Find( SplitterBox( 1 ) );
sb2 << Set Width( 400 );
// height
sb3 = window[SplitterBox(1)];
sb3 << set sizes({0.55,1,0.5}); // this sets the proportions of the 3 panels left hand side in a JMP table
//-------------------------------------------------------------------------------------------------	
// setting row states for rows where overfill is <5%rel. and <0%rel. relative to lower spec limit
// ref.: https://www.jmp.com/support/help/en/18.0/index.shtml#page/jmp/row-states.shtml
//-------------------------------------------------------------------------------------------------
RsltNLmts << Select Where( 0 <= :"Overfill, lower limit [%rel]"n < 5 );
RsltNLmts << Markers( 8 ); // ring
RsltNLmts << Select Where( :"Overfill, lower limit [%rel]"n < 0 );
RsltNLmts << Markers( 11 ); // star (*)
RsltNLmts << Clear Select;


//-----------------------------------------------------------------------------------	
// add event handler to batchid & LIMS sampleid columns ("Georg Raming's data cockpit"
// :https://community.jmp.com/t5/Abstracts/Data-Cockpit-Ways-to-Enrich-a-Master-Table-with-Additional-Data/ev-p/708109)
//-----------------------------------------------------------------------------------
// batchid
RsltNLmts:BatchId << Set Property(
	"Event Handler",
	Event Handler(
		Click(
			JSL Quote(
				Function( {thisTable, thisColumn, iRow}, // 3 parameters
					New SQL Query(
						Connection( "ODBC:DSN=bq64_system" ),
						CustomSQL( Eval Insert( "\[SELECT t1.* FROM akbm-datawarehouse-operational.views_qualityControl.FactQCFullHistory t1 where t1.BatchId='^Char( thisTable:thisColumn[ iRow ] )^' ;]\" ) )
					) << Run()
				);
			)					
		),
		Tip(
			JSL Quote(
				Function( {thisTable, thisColumn, iRow}, // 3 parameters
					"Get release data for " || Char( thisTable:thisColumn[ iRow ] ) || " on your machine."; // return the tool tip string
				);
			)		
		),
		Color(
			JSL Quote(
				Function( {thisTable, thisColumn, iRow}, // 3 parameters
					RGBColor("link"); // return the color for the link. RGBColor("link") is the preferred system link color (often a shade of blue).
				)
			)		
		)
	)
);
// sampleid
RsltNLmts:SampleId << Set Property(
	"Event Handler",
	Event Handler(
		Click(
			JSL Quote(
				Function( {thisTable, thisColumn, iRow}, // 3 parameters
					New SQL Query(
						Connection( "ODBC:DSN=bq64_system" ),
						CustomSQL( Eval Insert( "\[SELECT t1.* FROM akbm-datawarehouse-operational.views_qualityControl.FactQCFullHistory t1 where t1.BatchId='^Char( thisTable:thisColumn[ iRow ] )^' ;]\" ) )
					) << Run()
				);
			)					
		),
		Tip(
			JSL Quote(
				Function( {thisTable, thisColumn, iRow}, // 3 parameters
					"Get release data for " || Char( thisTable:thisColumn[ iRow ] ) || " on your machine."; // return the tool tip string
				);
			)		
		),
		Color(
			JSL Quote(
				Function( {thisTable, thisColumn, iRow}, // 3 parameters
					RGBColor("link"); // return the color for the link. RGBColor("link") is the preferred system link color (often a shade of blue).
				)
			)		
		)
	)
);




/*
//-------------------------------------------------------------------------------------------------	
// to do
//-------------------------------------------------------------------------------------------------
*) add fill wt development from CMO to fatty acid testing, for example. for many batches, we have now the average fill weight as determined per CMO as well as the fill weight from fatty acid testing in the table. in between these two we should see an increase of the fill weight due to migration of water and glycerol into the fill.

*) add water in fill. this is tested, as it seems, at least for lysoveta soft gels

*) the data as presented in "Overfill, relative to lower spec vs paramid, by batchid" could be used in a PCA. data should be arranged like this:

	|bx id | qc result_1 [%rel. to spec limit] | qc result_2 [%rel. to spec limit]|
	--------------------------------------------------------------------------------
	|  1   |           0,12                    |            0,34                  |
	|  2   |           0,56                    |            0,78                  |
	|  3   |           ....                    |            ....                  |
	|  ... |           ....                    |            ....                  |
	|  n   |           ....                    |            ....                  |


0.) test the hypothesis that lpc/pc vs mfg date is positively correlated with bulk oil age at the time of capsule mfg
		water, pH, tensides, ... are also possible factors
		also, do lpc/pc ratios change from bulk oil to capsule, as a result of encapuslation?

1.) yield calculations! these require:
		capsule volumes manufactured vs amount of oil used
		degradation correction of the KO
		weighted avg conc levels of the KO blend bx used for a capsule bx
		prescribed vs actual fill wt --> this info is in Jeeves, but maybe not yet in DWH table?

2.) similar to overfill, add a column which shows that capsules are breaking the upper spec limit, if one exists

3.) "data cockpit" a la Georg Raming for loading ALL capsule release data + corresponding spec from DWH (one table each?)
		maybe make modal window that lets user select between getting a subset table of the bx in question or query the DWH for everything that is available with regards to this batch

4.) considering moving the table scripts (graphing, etc.) into a separate section

5.) additional parameters to be added to script
		fill water 
		shell water

6.) make sure additional CMOs, products and parameters are added to this script as needed

7.) out of this script's scope: automate oil selection for capsule mfg


//-------------------------------------------------------------------------------------------------	
// good to know
//-------------------------------------------------------------------------------------------------

raw spec table has paramlistid and paramid
	paramid in raw spec table = TestDetail in FactQCFullHistory
	paramlistid in raw spec table seems to correspond with TestMethod in FactQCFullHistory, but agreement is not 100%
	
moving columns
	https://www.jmp.com/support/help/en/18.0/index.shtml#page/jmp/rearrange-and-move-columns.shtml
	
Spec-00134 has a spec for "Fill weight of astaxanthin (10%)" in mg/capsule and a spec limit for "Astaxanthin" in µg/capsules
	the "Fill weight of astaxanthin 10%" limit describes how much of a carrier oil containing 10% (w/w) of astaxanthin are to be filled in a capsule.
	we are not interested in this. we are only interested in the "Astaxanthin" limit, which is achieved by filling the 10% (w/w) Astaxanthin oil into the capsule.
	the same spec also has a limit for "Average fill weight of krill oil".
	presumably, "Average fill weight of krill oil" + "Fill weight of astaxanthin (10%)" = "Average fill weight", which is a real spec limit.
	this is what we're doing: well add the two together and calle the same "Average fill weight", in accordance with the other capsule products.

Average fill weight
	When TestName = 'Average fill weight', 'Average fill weight (3)' or 'Average fill weight (4)', this means the value is from the CMO.
	However, each of these TestNames variants is associated with a different method as shown below:
		'Average fill weight' 		= 'USP (capsules)'
		'Average fill weight (3)'	= 'European Pharmacopoeia (Capsugel)'
		'Average fill weight (4)'	= 'Calculation (capsules)'
	There is correspondence between CMO and method used. Calculation is only used by Lonza Greenwood at the time of writing.
	Comment #1: 	The values for 'Average fill weight (4)' appear hard to locate. For example, bx 6111834 has a fill wt of 727 mg/capsule per CMO CoA.
					But the value on our own release CoA for bx 6111834 is 727 mg/capsule (721.969 mg/capsule in the DWH).
					Other examples, like bx 6111693 can be found.
					
	Comment #2:		'European Pharmacopoeia (Capsugel)' was used for bx id 1533140100, which is a Swisscaps product

Fill weight from in-house testing in Houston
	When TestDetail = 'Capsule fill weight' and TestName = 'Fatty acid profile (6)', this fill weight was determined as part of fatty acid quantification in Houston.

Capsule bx ID 2002493 exists in Jeeves but not in FactQCFullhistory. On Box there is also no AKBM CoA for this HGC bx.






//-------------------------------------------------------------------------------------------------	
// OTHER NOTES
//-------------------------------------------------------------------------------------------------
Personal / hand written notes related to the topic of capsule trending are available from
	22.Apr.'24



